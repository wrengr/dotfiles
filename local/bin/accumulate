#!/usr/bin/env perl
# This program is a modified version of:
#     <http://blog.plover.com/prog/accumulate.html>
# wren gayle romano <wren@cpan.org>                 ~ 2012.02.19
# ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
#
# This is especially helpful for things like:
# $> md5sum * | accumulate
#
# TODO: is it worth having a flag to:
# * disable the silencing of undefined $F[$opt{v}]
# * sort the keys (instead of relying on calling `sort` afterwards)
# * removing the lags for -k and -v, a la MJD's comments?
# * allow specifying other ways to split columns, besides just
#   assuming split-on-space.

use warnings;
use strict;
use Getopt::Std;

my %opt = (k => 1, v => 2, u => 0, U => 0);
getopts('k:v:uU?', \%opt) or HELP_MESSAGE();
HELP_MESSAGE() if $opt{'?'};
if ($opt{u} and $opt{U}) {
	print STDERR "$0: Error: Cannot combine -u with -U\n";
	exit 1;
}
for (qw(k v)) {
    $opt{$_} -= 1 if $opt{$_} > 0;
}

my %K;
while (<>) {
	chomp;
	my @F = split;
	push @{$K{$F[$opt{k}]}}, $F[$opt{v}]
		if defined $F[$opt{v}];
}

for my $k (keys %K) {
	print "$k @{$K{$k}}\n"
		if  (not $opt{u} or 1 == @{$K{$k}})
		and (not $opt{U} or 1 != @{$K{$k}});
}

sub HELP_MESSAGE {
	print STDERR "Usage: ... | $0 [flags]
	-k N    set the key column to N   (default: 1)
	-v N    set the value column to N (default: 2)
	-u      only print keys with unique values
	-U      only print keys with non-unique values
	-?      print this help message
";
	exit 1;
}

1;
__END__
